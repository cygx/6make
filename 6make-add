#!/usr/bin/env perl6

my $DIR := $*SIXMAKE-DIR // $?FILE.IO.parent.abspath;
my &get := EVALFILE("$DIR/6make-get.plugin");

sub parse($key) {
    rx/^ <[\h\,\{\}]>* '"' $key '"' \h* ':' \h* '"' (<-["]>*) '"' /
}

my %repos = "$DIR/repo.list".IO.lines.map({ |.split(/\h+/, 2) });
my @failures;

my $n = 0;
my $N = +@*ARGS;
for @*ARGS -> $target {
    ++$n;
    my ($name, $url);
    for get($target).lines {
        when (BEGIN parse 'name') { $name = ~$0 }
        when (BEGIN parse 'source-url') { $url = ~$0 }
    }

    if $name && $url {
        say "       $n/$N $name";
        %repos{ $name.lc.subst(:g, '::', '-') } = $url;
    }
    else {
        @failures.push($target);
        note "FAILED $target";
    }
}

if @failures {
    note "\nThe following targets failed to parse:\n";
    .note for @failures;
}

my @keys = %repos.keys.sort;
my $ws = @keys>>.chars.max + 1;
my $lines = @keys.map({ [~] $_, ' ' x ($ws - .chars), %repos{$_}, "\n" });
"$DIR/repo.list".IO.spurt($lines.join);
